#!/usr/bin/perl
# -*- coding: None -*-
# vim: sw=4 sts=4:

# zabbix_multichecker: Execute and parse the output of a command and return
# some selected values as Zabbix items to the Zabbix server. A configuration
# file is used to determind the command to execute and the items to return.
#
# Date: 2007/05/17
# Version: 1.0
#
# Copyright (C) 2007 Farzad FARID <ffarid@pragmatic-source.com>
#
#  This program is free software; you can redistribute it and/or modify
#  it under the terms of the GNU General Public License as published by
#  the Free Software Foundation; either version 2 of the License, or
#  (at your option) any later version.
#
#  This program is distributed in the hope that it will be useful,
#  but WITHOUT ANY WARRANTY; without even the implied warranty of
#  MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
#  GNU General Public License for more details.
#
# You should have received a copy of the GNU General Public License
# along with this program; if not, write to the Free Software
# Foundation, Inc., 51 Franklin St, Fifth Floor, Boston, MA  02110-1301 USA
#
# TODO:
# - Support multiple items per regular expression. For this we need to correctly parse 
#   number of parentheses in the regex (or count the number of item names on the same 
#   line in the configuration file) and provide the corresponding $1, $2, $3...
# - 
use warnings;
use strict;
use Data::Dumper;
use Getopt::Long;
use Pod::Usage;

# Default Zabbix Agentd and Check configuration files
my $default_agentd_conf = "/etc/zabbix/zabbix_agentd.conf";
my $default_multichecker_conf = "/etc/zabbix/zabbix_multichecker.conf";

# These default values are taken from the default zabbix configuration files
# or are reasonnable defaults
my $default_server = "127.0.0.1";
my $default_sport = "10051";
my $sender_bin = "/usr/local/bin/zabbix_sender";

#----------------------------------------------
# Do not change anything below this line
# ---------------------------------------------
my $agentd_conf = $default_agentd_conf;
my $multichecker_conf = $default_multichecker_conf;
my $man = 0;
my $help = 0;
my $debug = 0;

# Print debug messages, only if $debug > 0
sub debug {
    return if !$debug;

    my $msg = $_[0];

    # Get some info on the calling procedure. A call from main is a special
    # case.
    my @info = caller(1);
    my $subroutine_name = $info[3] || "main";
    
    foreach my $line (split(/\n/, $msg)) {
	chomp($line);
        print STDERR "DEBUG: ($subroutine_name) $line\n";
    }
}

# Parse the Zabbix Agentd configuration file to get the local hostname,
# the server IP or name and the server port. This way, we don't have to
# redefine them.
# Return value:
#    A array { hostname, servername, serverport }
sub parse_agentd_conf {
    my $conffile = $_[0];
    # Some parameters can have a default value
    my ($server, $sport, $hostname) = ($default_server, $default_sport, undef);

    debug("Parsing main Zabbix Agentd configuration file '$conffile'");
    open (my $fh, "<$conffile") or die "ERROR: Can't open Zabbix agentd's configuration file '$conffile'";
    while (<$fh>) {
	$server = $1 if (m/^\s*Server\s*=\s*([^\s,]*)/);
	$sport = $1 if (m/^\s*ServerPort\s*=\s*(.*)/);
	$hostname = $1 if (m/^\s*Hostname\s*=\s*(.*)/);
    }
    close($fh);

    # Hostname must be defined, we can't guess a default value
    if (!defined($hostname)) {
	die("ERROR: Hostname must be defined in the Zabbix Agentd configuration file");
    }

    my $result = {"hostname" => $hostname, "servername" => $server, "serverport" => $sport};
    debug("Parse result = " . Dumper($result));
    return $result;
}

# Parse the Zabbix Multichecker configuration file. This file contains:
# - the command to execute. It will be run in a shell and its ouput will
#   then be parsed.
# - A list of regular expressions and their corresponding items.
# Return value:
#  Return an array
#  - "command" => "command to execute"
#  - "items" => hash of { "regexp" => "item" }
# TODO:
# - For the moment only parses one item per regexp
# - Does not accept the [] syntax in the item name
sub parse_multichecker_conf {
    my $conffile = $_[0];
    my $command = undef;
    my %items;

    debug("Parsing Multichecker configuration file '$conffile'");
    open (my $fh, "<$conffile") or die "ERROR: Can't open Zabbix Multichecker's configuration file '$conffile'";
    while (<$fh>) {
	chomp;
	if (m/^(#|$)/) {
	    # Comment or blank line
	    next;
	} elsif (m/^\s*command\s*=\s*(.*)$/) {
	    # Parse command line, which must only be defined once
	    if (!defined($command)) {
		$command = $1;
	    } else {
		die("ERROR: Command line redefined in multichecker configuration file '$conffile'");
	    }
	} elsif (m/^\s*item\s*=\s*\/(.*)\/\s*,\s*([[:alnum:].]+)\s*$/) {
	    # Parse regexp/item line
	    $items{$1} = $2;
	} else {
	    # Unparseable line
	    die "ERROR: Syntax error in multichecker configuration file '$conffile': \"$_\"";
	}
    }
    close($fh);

    if (!defined($command) or !scalar(%items)) {
	die "ERROR: Missing command and/or item lines in multichecker configuration file '$conffile'";
    }

    my $config = { "command" => $command, "items" => \%items };
    debug("Parse result = " . Dumper($config));
    return $config;
}

# Parse the command line
Getopt::Long::Configure(qw(gnu_getopt no_ignore_case));
GetOptions('help|h|?' => \$help, man => \$man, 'debug|d' => \$debug,
    'agent-conf|A=s' => \$agentd_conf,
    'multichecker-conf|M=s' => \$multichecker_conf,
    'sender|S=s' => \$sender_bin) or pod2usage(2);
pod2usage(1) if $help;
pod2usage(-exitstatus => 0, -verbose => 2) if $man;

# zabbix_sender must be available
if (! -r $sender_bin) {
    pod2usage(-message => "ERROR: Invalid 'zabbix_sender' path '$sender_bin'",
	-exitval => 3);
}

# Parse the two configuration files
my $agentd = &parse_agentd_conf($agentd_conf);
my $multiconfig = &parse_multichecker_conf($multichecker_conf);

# Execute the command
debug("Executing '$multiconfig->{command}'");
my $result = `$multiconfig->{command}`;
debug("==== Command result ====");
debug($result);
debug("==== End command result ====");

# And now parse the output and compare to regular expressions
debug("==== Parsing output and generating items ====");
while (my ($reg,$item) = each(%{$multiconfig->{items}})) {
    debug(" === Matching against '$reg' ===");
    if ($result =~ m/$reg/m) {
	debug("  == Found a match! Setting the item '$item' to '$1' ==");
	# Now send the result back to the Zabbix Server
	my $send_cmd = "$sender_bin --zabbix-server $agentd->{servername} --port $agentd->{serverport} --host $agentd->{hostname} --key '$item' --value '$1'";
	debug("   = Send command is : $send_cmd");
	`$send_cmd` if (!$debug);
    }
}
debug("==== End output parsing ====");

print "OK\n";
exit 0;

__END__

=head1 NAME

zabbix_multichecker - A tool used to execute a serie of Unix commands and then
parse the command's output to create a number of Zabbix C<items>. It then
uses C<zabbix_sender> to send the items back to the Zabbix server.

=head1 SYNOPSIS

zabbix_multichecker [options]

Options:

 --agent-conf=FILE, -A FILE	Path to the Zabbix Agentd configuration file
 --multichecker-conf=FILE, -M FILE	Path to zabbix_multichecker's configuration file
 --sender=FILE, -S FILE		Path of the zabbix_sender executable
 --debug, -d			Print more information (do not use in production!)
 --help|-h			Brief help message
 --man				Full documentation

=head1 OPTIONS

=over 5

=item B<--agent-conf=I<FILE>|-A I<FILE>> 

Path of the Zabbix Agentd configuration file. This file is parsed to get the
serveur name and port, and the local hostname.
Defaults to '/etc/zabbix/zabbix_agentd.conf'.

=item B<--multichecker-conf=I<FILE>|-M I<FILE>>

Path of zabbix_multichecker's configuration file. The file contains the command to
execute and the list of regexp/items.
Defaults to C</etc/zabbix/zabbix_multichecker.conf>.

=item B<--sender=I<FILE>|-S I<FILE>>

Path of the C<zabbix_sender> executable. Defaults to C</usr/local/bin/zabbix_sender>.

=item B<--debug|-d>

Print more information on the execution of the script. Do not use in production
as it disturb the output processing!

=item B<--help|-h>

Prints a brief help message and exits.

=item B<--man>

Prints the manual page and exists.

=back

=head1 DESCRIPTION

B<zabbix_multichecker> parses a specific configuration file and executes in a
shell the defined command.

A list of regular expression is then executed on the output, and each
corresponding match generates an item and its corresponding value. The items
are then returned back to the server with the B<zabbix_agentd> program.

=head1 AUTHOR

Author: Farzad FARID <ffarid@pragmatic-source.com>

For more information or support check http://www.pragmatic-source.com/ .

=cut

